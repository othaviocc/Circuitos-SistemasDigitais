*SIMULACAO DE UMA XOR

.include 32nm_HP.pm


*Declaração das Fontes

Vvdd vdd gnd 0.9

*fontes que estimulam os atrasos
Va a gnd PWL(0n 0 2n 0 4n 0 6n 0 6.01n 0.9 8n 0.9 8.01n 0 10n 0 10.01n 0.9 12n 0.9 12.01n 0 14n 0 14.01n 0.9)
Vb b gnd PWL(0n 0 2n 0 2.01n 0.9 4n 0.9 4.01n 0 10n 0 10.01n 0.9 16n 0.9 16.01n 0 18n 0 18.01n 0.9)


*fontes que testam a tabela verdade da função
*Va a gnd PWL(0n 0 3n 0 3.01n 0.9 5n 0.9)
*Vb b gnd PWL(0n 0 1n 0 1.01n 0.9 3n 0.9 3.01n 0 5n 0 5.01n 0.9)



*Declaração do Netlist

*inversor entrada A
Mp1 vdd a a_neg vdd PMOS w=105n L=32n
Mn1 gnd a a_neg gnd NMOS w=70n L=32n

*inversor entrada B
Mp2 vdd b b_neg vdd PMOS w=105n L=32n
Mn2 gnd b b_neg gnd NMOS w=70n L=32n

*porta XOR
Mp3 vdd a_neg x1 vdd PMOS w=105n L=32n
Mp4 vdd b x1 vdd PMOS w=105n L=32n
Mp5 x1 a s vdd PMOS w=105n L=32n
Mp6 x1 b_neg s vdd PMOS w=105n L=32n

Mn3 gnd b_neg x3 gnd NMOS w=70n L=32n
Mn4 gnd b x4 gnd NMOS w=70n L=32n
Mn5 x3 a s gnd NMOS w=70n L=32n
Mn6 x4 a_neg s gnd NMOS w=70n L=32n

*inversor na saida
Mp7 vdd s saida vdd PMOS w=105n L=32n
Mn7 gnd s saida gnd NMOS w=70n L=32n


*capacitor acoplado na saída simulando uma carga
Cload saida gnd 1f 

*Tipo de Simulação

.tran 0.001n 20n

**AINDA FALTAM 4 ATRASOS PARA SEREM MEDIDOS
.control
	set xbrushwidth=3
	set color0=white
	run
	plot a+4 b+2 saida
	meas tran tplh_A0_B01 trig v(b) val=0.45 rise=1 targ v(saida) val=0.45 rise=1
	meas tran tphl_A0_B10 trig v(b) val=0.45 fall=1 targ v(saida) val=0.45 fall=1
	meas tran tplh_A01_B0 trig v(a) val=0.45 rise=1 targ v(saida) val=0.45 rise=2
	meas tran tphl_A10_B0 trig v(a) val=0.45 fall=1 targ v(saida) val=0.45 fall=2	
.endc


.end
